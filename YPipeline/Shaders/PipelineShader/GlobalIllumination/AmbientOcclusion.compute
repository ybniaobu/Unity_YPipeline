#pragma kernel SSAOKernel

#define THREAD_NUM_X 32
#define THREAD_NUM_Y 8

#include "../../../ShaderLibrary/Core/YPipelineCSCore.hlsl"
#include "../../../ShaderLibrary/RandomLibrary.hlsl"
#include "../../../ShaderLibrary/SamplingLibrary.hlsl"
#include "../../../ShaderLibrary/EncodingLibrary.hlsl"

TEXTURE2D(_CameraDepthTexture);
TEXTURE2D(_ThinGBufferTexture);
TEXTURE2D(_BlueNoise64);
float4 _BlueNoise64_TexelSize;
SAMPLER(sampler_PointClamp);

RWTexture2D<float4> _AmbientOcclusionTexture;

float4 _CameraBufferSize; // x: 1.0 / bufferSize.x, y: 1.0 / bufferSize.y, z: bufferSize.x, w: bufferSize.y
float4 _AmbientOcclusionParams; // x: sample count, y: radius

// ----------------------------------------------------------------------------------------------------
// Input
// ----------------------------------------------------------------------------------------------------

float GetSSAOSampleCount()      { return _AmbientOcclusionParams.x; }
float GetSSAORadius()           { return _AmbientOcclusionParams.y; }

// ----------------------------------------------------------------------------------------------------
// Kernels
// ----------------------------------------------------------------------------------------------------

[numthreads(THREAD_NUM_X, THREAD_NUM_Y, 1)]
void SSAOKernel(uint3 id : SV_DispatchThreadID)
{
    bool inScreen = (float) id.x < _CameraBufferSize.z && (float) id.y < _CameraBufferSize.w;
    if (!inScreen) return;
    
    // ------------------------- Get Depth & Normal -------------------------
    
    float originDepth = LOAD_TEXTURE2D_LOD(_CameraDepthTexture, id.xy, 0).r;
    float4 NDC = GetNDCFromUVAndDepth(id.xy * _CameraBufferSize.xy, originDepth);
    float3 originWS = TransformNDCToWorld(NDC, UNITY_MATRIX_I_VP);

    float3 packNormalWS = LOAD_TEXTURE2D_LOD(_ThinGBufferTexture, id.xy, 0).rgb;
    float3 normalWS = normalize(DecodeNormalFrom888(packNormalWS));

    // ------------------------- Generate Normal-Oriented Hemisphere Samples -------------------------
    
    float randomRadian = LOAD_TEXTURE2D_LOD(_BlueNoise64, id.xy % _BlueNoise64_TexelSize.w, 0).r * TWO_PI;
    float2x2 rotation = float2x2(cos(randomRadian), -sin(randomRadian), sin(randomRadian), cos(randomRadian));

    float occlusion = 0.0;
    float sampleCount = GetSSAOSampleCount();
    float radius = GetSSAORadius();

    UNITY_UNROLL
    for (int i = 0; i < sampleCount; i++)
    {
        float2 xi = k_Halton[i + 1];
        float3 dirTS = InverseSampleHemisphere(xi).xyz;
        float random = k_Sobol[i + 1].y;
        random = random * random;
        dirTS *= random;
        dirTS.xz = mul(rotation, dirTS.xz);

        float3 dirWS = TangentCoordToWorldCoord(dirTS, normalWS);
        float3 positionWS = originWS + dirWS * radius;
        float4 positionHCS = TransformWorldToHClip(positionWS);
        positionHCS.xyz /= positionHCS.w;
        float2 uv = positionHCS.xy * 0.5 + 0.5;

        #if UNITY_UV_STARTS_AT_TOP
        uv.y = 1.0f - uv.y;
        #endif
        
        float sampledDepth = LOAD_TEXTURE2D_LOD(_CameraDepthTexture, uv * _CameraBufferSize.zw, 0).r;
        float linearDepth = GetViewDepthFromDepthTexture(sampledDepth);
        
        bool rangeCheck = abs(positionHCS.w - linearDepth) < radius;
        
        occlusion += (linearDepth < positionHCS.w) * rangeCheck;
    }

    occlusion = 1 - (occlusion / sampleCount);

    _AmbientOcclusionTexture[id.xy] = occlusion;
}

