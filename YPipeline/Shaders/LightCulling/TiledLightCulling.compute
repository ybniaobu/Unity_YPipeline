#pragma kernel TiledLightCulling

#define THREAD_NUM_X 16 // equal to per tile size
#define THREAD_NUM_Y 16

#include "../../ShaderLibrary/Core/YPipelineCSCore.hlsl"

TEXTURE2D(_CameraDepthTexture);
SAMPLER(sampler_PointClamp);

StructuredBuffer<float4> _LightsCullingInputBuffer; // xyz: light position, w: light range

float4 _CameraBufferSize; // x: 1.0 / bufferSize.x, y: 1.0 / bufferSize.y, z: bufferSize.x, w: bufferSize.y
float4 _PunctualLightCount; // x: punctual light count, yzw: 暂无

float4 _TileParams; // xy: tileCountXY, zw: tileUVSizeXY
float4 _CameraNearPlaneLB; // xyz: near plane left bottom point position in view space
float4 _TileNearPlaneSize; // xy: tile near plane size in view/world space

RWStructuredBuffer<uint> _TilesLightIndicesBuffer;

groupshared uint tileMinDepthInt;
groupshared uint tileMaxDepthInt;
groupshared uint lightCountInTile;
groupshared uint lightIndicesInTile[MAX_PUNCTUAL_LIGHT_COUNT];

// ----------------------------------------------------------------------------------------------------
// Input
// ----------------------------------------------------------------------------------------------------

float3 GetPunctualLightPosition(uint lightIndex)    { return _LightsCullingInputBuffer[lightIndex].xyz; }
float GetPunctualLightRange(uint lightIndex)        { return _LightsCullingInputBuffer[lightIndex].w; }
float GetPunctualLightCount()                       { return _PunctualLightCount.x; }

// ----------------------------------------------------------------------------------------------------
// Intersect Test Referred Functions
// ----------------------------------------------------------------------------------------------------

void GetTileFrustumCorners(out float3 tileCorners[4], uint2 tileIndex)
{
    tileCorners[0] = _CameraNearPlaneLB.xyz + tileIndex.x * float3(_TileNearPlaneSize.x, 0, 0) + tileIndex.y * float3(0, _TileNearPlaneSize.y, 0);
    tileCorners[1] = tileCorners[0] + float3(0, _TileNearPlaneSize.y, 0);
    tileCorners[2] = tileCorners[1] + float3(_TileNearPlaneSize.x, 0, 0);
    tileCorners[3] = tileCorners[0] + float3(_TileNearPlaneSize.x, 0, 0);
}

bool DepthIntersect(float3 lightPositionVS, float lightRange)
{
    float tileDepthMin = asfloat(tileMinDepthInt);
    float tileDepthMax = asfloat(tileMaxDepthInt);

    float lightDepthMin = -lightPositionVS.z - lightRange;
    float lightDepthMax = -lightPositionVS.z + lightRange;
    
    return lightDepthMin <= tileDepthMax && lightDepthMax >= tileDepthMin;
}

// ----------------------------------------------------------------------------------------------------
// Sphere-Frustum Intersect Test
// ----------------------------------------------------------------------------------------------------

bool SidePlanesIntersect(float3 p1, float3 p2, float3 lightPositionVS, float lightRange)
{
    float3 N = -normalize(cross(p1, p2));
    float distance = dot(N, lightPositionVS);
    return distance < lightRange;
}

bool SphereFrustumIntersectTest(uint lightIndex, uint2 tileIndex)
{
    UNITY_BRANCH
    if ((float) lightIndex >= GetPunctualLightCount())
    {
        return false;
    }
    else
    {
        float3 lightPositionVS = TransformWorldToView(GetPunctualLightPosition(lightIndex));
        float lightRange = GetPunctualLightRange(lightIndex);

        float3 tileCorners[4];
        GetTileFrustumCorners(tileCorners, tileIndex);

        bool sideIntersected0 = SidePlanesIntersect(tileCorners[0], tileCorners[1], lightPositionVS, lightRange);
        bool sideIntersected1 = SidePlanesIntersect(tileCorners[1], tileCorners[2], lightPositionVS, lightRange);
        bool sideIntersected2 = SidePlanesIntersect(tileCorners[2], tileCorners[3], lightPositionVS, lightRange);
        bool sideIntersected4 = SidePlanesIntersect(tileCorners[3], tileCorners[0], lightPositionVS, lightRange);
        return sideIntersected0 && sideIntersected1 && sideIntersected2 && sideIntersected4 && DepthIntersect(lightPositionVS, lightRange);
    }
}

// ----------------------------------------------------------------------------------------------------
// Cone Intersect Test
// From https://lxjk.github.io/2018/03/25/Improve-Tile-based-Light-Culling-with-Spherical-sliced-Cone.html#_spherical_sliced_cone_test
// ----------------------------------------------------------------------------------------------------

bool TileLightConeIntersect(float3 tileCorners[4], float3 lightPositionVS, float lightRange)
{
    float3 tileCenterVec = normalize(tileCorners[0] + tileCorners[1] + tileCorners[2] + tileCorners[3]);
    float tileCos = min(min(min(dot(tileCenterVec, normalize(tileCorners[0])), dot(tileCenterVec, normalize(tileCorners[1]))), dot(tileCenterVec, normalize(tileCorners[2]))), dot(tileCenterVec, normalize(tileCorners[3])));
    float tileSin = sqrt(1 - tileCos * tileCos);

    float lightDistSqr = dot(lightPositionVS, lightPositionVS);
    float lightDist = sqrt(lightDistSqr);
    float3 lightCenterVec = lightPositionVS / lightDist;
    float lightSin = clamp(lightRange / lightDist, 0.0, 1.0);
    float lightCos = sqrt(1 - lightSin * lightSin);
    
    float lightTileCos = dot(lightCenterVec, tileCenterVec);
    float sumCos = (lightRange > lightDist) ? -1.0 : (tileCos * lightCos - tileSin * lightSin);

    return lightTileCos >= sumCos;
}

bool ConeIntersectTest(uint lightIndex, uint2 tileIndex)
{
    UNITY_BRANCH
    if ((float) lightIndex >= GetPunctualLightCount())
    {
        return false;
    }
    else
    {
        float3 lightPositionVS = TransformWorldToView(GetPunctualLightPosition(lightIndex));
        float lightRange = GetPunctualLightRange(lightIndex);
        
        float3 tileCorners[4];
        GetTileFrustumCorners(tileCorners, tileIndex);

        return TileLightConeIntersect(tileCorners, lightPositionVS, lightRange) && DepthIntersect(lightPositionVS, lightRange);
    }
}

// ----------------------------------------------------------------------------------------------------
// Spherical-Sliced Cone Test
// Also From https://lxjk.github.io/2018/03/25/Improve-Tile-based-Light-Culling-with-Spherical-sliced-Cone.html#_spherical_sliced_cone_test
// ----------------------------------------------------------------------------------------------------

bool MinMaxDistanceIntersect(float3 tileCorners[4], float3 lightPositionVS, float lightRange)
{
    float3 tileCornerVec0 = normalize(tileCorners[0]);
    float3 tileCornerVec1 = normalize(tileCorners[1]);
    float3 tileCornerVec2 = normalize(tileCorners[2]);
    float3 tileCornerVec3 = normalize(tileCorners[3]);

    float3 tileCenterVec = normalize(tileCornerVec0 + tileCornerVec1 + tileCornerVec2 + tileCornerVec3);
    float tileCos = min(min(min(dot(tileCenterVec, tileCornerVec0), dot(tileCenterVec, tileCornerVec1)), dot(tileCenterVec, tileCornerVec2)), dot(tileCenterVec, tileCornerVec3));
    float tileSin = sqrt(1 - tileCos * tileCos);

    float lightDistSqr = dot(lightPositionVS, lightPositionVS);
    float lightDist = sqrt(lightDistSqr);
    float3 lightCenterVec = lightPositionVS / lightDist;

    float lightTileCos = dot(lightCenterVec, tileCenterVec);
    float lightTileSin = sqrt(1 - lightTileCos * lightTileCos);

    float diffSin = clamp(lightTileSin * tileCos - lightTileCos * tileSin, 0.0, 1.0);
    float diffCos = (diffSin == 0.0) ? 1.0 : lightTileCos * tileCos + lightTileSin * tileSin;
    float lightTileDistOffset = sqrt(lightRange * lightRange - lightDistSqr * diffSin * diffSin);
    float lightTileDistBase = lightDist * diffCos;

    float tileDepthMin = asfloat(tileMinDepthInt);
    float tileDepthMax = asfloat(tileMaxDepthInt);
    float cosCornerMin = min(min(min(-tileCornerVec0.z, -tileCornerVec1.z), -tileCornerVec2.z), -tileCornerVec3.z);
    float cosCornerMax = max(max(max(-tileCornerVec0.z, -tileCornerVec1.z), -tileCornerVec2.z), -tileCornerVec3.z);
    float tileDistMin = tileDepthMin / cosCornerMax;
    float tileDistMax = tileDepthMax / cosCornerMin;

    return lightTileDistBase - lightTileDistOffset < tileDistMax && lightTileDistBase + lightTileDistOffset > tileDistMin;
}

bool SphericalSlicedConeIntersectTest(uint lightIndex, uint2 tileIndex)
{
    UNITY_BRANCH
    if ((float) lightIndex >= GetPunctualLightCount())
    {
        return false;
    }
    else
    {
        float3 lightPositionVS = TransformWorldToView(GetPunctualLightPosition(lightIndex));
        float lightRange = GetPunctualLightRange(lightIndex);

        float3 tileCorners[4];
        GetTileFrustumCorners(tileCorners, tileIndex);

        return MinMaxDistanceIntersect(tileCorners,lightPositionVS, lightRange);
    }
}

// ----------------------------------------------------------------------------------------------------
// AABB Intersect Test
// ----------------------------------------------------------------------------------------------------

// bool IntersectAABB(float3 tileCorners[4], float3 lightPositionVS, float lightRange)
// {
//     tileCorners[0]
//     float2 tileCenter = (tileCorners[0].xy + tileCorners[2].xy) * 0.5f;
//     float2 lightCenter = lightPositionVS.xy;
//     float2 delta = tileCenter - lightCenter;
//     float dist = length(delta);
//     
//     float a = length(tileCorners[0].xy - tileCenter);
//     return dist < a + lightRange;
// }

bool AABBIntersectTest(uint lightIndex, uint2 tileIndex)
{
    if ((float) lightIndex >= GetPunctualLightCount()) return false;
    
    float3 lightPositionVS = TransformWorldToView(GetPunctualLightPosition(lightIndex));
    float lightRange = GetPunctualLightRange(lightIndex);
    
    float tileDepthMin = asfloat(tileMinDepthInt);
    float tileDepthMax = asfloat(tileMaxDepthInt);

    float3 tileCorners[4];
    GetTileFrustumCorners(tileCorners, tileIndex);

    float tileDepthMinScale = tileDepthMin / _ProjectionParams.y;
    float tileDepthMaxScale = tileDepthMax / _ProjectionParams.y;
    float3 tileCorner0 = tileCorners[0] * tileDepthMinScale;
    float3 tileCorner2 = tileCorners[2] * tileDepthMaxScale;

    float3 tileCenter = 0.5 * (tileCorner0 + tileCorner2);
    float dist = length(tileCenter - lightPositionVS);

    float a = length(tileCenter - tileCorner0);

    return dist < a + lightRange;
}

// ----------------------------------------------------------------------------------------------------
// Kernel
// ----------------------------------------------------------------------------------------------------

[numthreads(THREAD_NUM_X, THREAD_NUM_Y, 1)]
void TiledLightCulling(uint3 id : SV_DispatchThreadID, uint3 groupThreadId : SV_GroupThreadID, uint3 groupId : SV_GroupID, uint groupIndex : SV_GroupIndex)
{
    // initialize shared memory
    if (groupIndex == 0)
    {
        tileMinDepthInt = 0x7f7fffff;
        tileMaxDepthInt = 0;
        lightCountInTile = 0;
    }

    GroupMemoryBarrierWithGroupSync();

    // get the minimum and maximum depth of the tile
    bool inScreen = (int) id.x < _CameraBufferSize.z && (int) id.y < _CameraBufferSize.w;

    if (inScreen)
    {
        float depth = LOAD_TEXTURE2D_LOD(_CameraDepthTexture, id.xy, 0).r;
        float linearDepth = GetViewDepthFromDepthTexture(depth);
        uint z = asuint(linearDepth);
        InterlockedMin(tileMinDepthInt, z);
        InterlockedMax(tileMaxDepthInt, z);
    }

    GroupMemoryBarrierWithGroupSync();
    
    // intersect test
    // if (SphereFrustumIntersectTest(groupIndex, groupId.xy))
    // if (ConeIntersectTest(groupIndex, groupId.xy))
    if (AABBIntersectTest(groupIndex, groupId.xy))
    // if (SphericalSlicedConeIntersectTest(groupIndex, groupId.xy))
    {
        uint offset;
        InterlockedAdd(lightCountInTile, 1, offset);
        lightIndicesInTile[offset] = groupIndex;
    }
    
    GroupMemoryBarrierWithGroupSync();

    // recording light indices
    uint headerIndex = (groupId.y * _TileParams.x + groupId.x) * (MAX_LIGHT_COUNT_PER_TILE + 1);
    uint minLightCount = min(lightCountInTile, MAX_LIGHT_COUNT_PER_TILE);

    UNITY_BRANCH
    if (groupIndex == 0)
    {
        _TilesLightIndicesBuffer[headerIndex] = lightCountInTile;
    }
    else
    {
        if (groupIndex - 1 < minLightCount) _TilesLightIndicesBuffer[headerIndex + groupIndex] = lightIndicesInTile[groupIndex - 1];
    }
}